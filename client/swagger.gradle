ext {
}

dependencies {
	//Need to include libraries for generated code to work
	implementation 'com.google.code.gson:gson:2.9.1'
	implementation 'io.gsonfire:gson-fire:1.8.5'

	implementation 'com.squareup.okhttp3:okhttp:4.10.0'
	implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'

	implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
	implementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'
}

openApiGenerate {
	inputSpec = "$rootDir//service/src/main/resources/static/swagger/openapi-docs.yaml".toString()
	outputDir = "$buildDir/generated".toString()
	generatorName = 'java'
	library = 'okhttp-gson' // the default
	configOptions = [
			// useRuntimeException: "true", // TODO: a change, but a good one?
			disallowAdditionalPropertiesIfNotPresent: "false",

			useJakartaEe: "true", // for compatibility with Spring Boot 3

			prependFormOrBodyParameters: "true", // orders arguments in the Java client same as previous swagger-codegen

			dateLibrary: "java8" // TODO: this is the default; delete?
	]
	invokerPackage = "${artifactGroup}.client".toString()
	modelPackage = "${artifactGroup}.model".toString()
	apiPackage = "${artifactGroup}.api".toString()
//	ignoreFileOverride = '${projectDir}/.swagger-codegen-ignore'
	// we may want to generate tests or doc later ...
	generateModelTests = false
	generateModelDocumentation = false
	generateApiTests = false
	generateApiDocumentation = false
}

idea.module.generatedSourceDirs = [file("${openApiGenerate.outputDir.get()}/src/main")]
sourceSets.main.java.srcDir files("${openApiGenerate.outputDir.get()}/src/main")
compileJava.dependsOn tasks.getByName('openApiGenerate')
sourcesJar.dependsOn tasks.getByName('openApiGenerate')
