import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'org.springframework.boot'
    id 'com.google.cloud.tools.jib'
    id 'org.sonarqube'
    id 'com.gorylenko.gradle-git-properties'
    id 'org.openapi.generator'
    id 'com.diffplug.spotless'
    id 'jacoco'
    id "au.com.dius.pact" version "4.6.17"
    id 'jvm-test-suite'
    id 'antlr'
}

springBoot {
    buildInfo()
}

repositories {
    maven { url 'https://broadinstitute.jfrog.io/artifactory/maven-central' }
    mavenCentral()
    maven { url 'https://broadinstitute.jfrog.io/artifactory/libs-snapshot' }
    maven { url 'https://broadinstitute.jfrog.io/artifactory/libs-release' }
}

jib {
    from {
        // see https://github.com/broadinstitute/dsp-appsec-blessed-images/tree/main/jre
        image = "us.gcr.io/broad-dsp-gcr-public/base/jre:17-debian"
    }

    container {
        mainClass = 'org.databiosphere.workspacedataservice.WorkspaceDataServiceApplication'
        creationTime = "USE_CURRENT_TIMESTAMP"
    }
}

ext {
    parquet_mr_version = "1.15.0"
    hadoop_version = "3.4.1"
    spring_cloud_gcp_version = "6.0.1"
}

dependencies {
    antlr "org.antlr:antlr4:4.13.2" // use ANTLR version 4

    // logging
    implementation 'org.zalando:logbook-spring-boot-starter:3.11.0'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'org.springframework.integration:spring-integration-jdbc'
    implementation 'org.aspectj:aspectjweaver' // required by spring-retry, not used directly
    implementation 'com.google.guava:guava:33.4.0-jre'
    implementation 'org.postgresql:postgresql'
    implementation 'org.webjars:webjars-locator-lite' // versioned by spring dependency management
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    implementation 'io.sentry:sentry-logback:8.3.0'
    implementation 'net.logstash.logback:logstash-logback-encoder:8.0'
    implementation 'org.codehaus.janino:janino'
    implementation 'org.liquibase:liquibase-core'
    implementation 'javax.cache:cache-api'
    implementation 'org.ehcache:ehcache:3.10.8:jakarta'
    implementation 'org.hashids:hashids:1.0.3'
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api'
    implementation 'com.google.mug:mug:8.5'
    implementation 'org.apache.lucene:lucene-queryparser:9.12.1'

    // required by openapi-generated models and api interfaces
    implementation 'jakarta.validation:jakarta.validation-api'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.28'

    // Terra libraries
    implementation group: 'org.broadinstitute.dsde.workbench', name: 'sam-client_2.13', version: 'v0.0.282'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0' // required by Sam client
    implementation "bio.terra:datarepo-client:2.13.0-SNAPSHOT"
    // WSM client included for models only
    implementation "bio.terra:workspace-manager-client:0.254.983-SNAPSHOT"
    implementation "bio.terra:java-pfb-library:0.69.0"
    implementation project(path: ':client')

    // Parquet, used for TDR snapshot import
    implementation "org.apache.parquet:parquet-common:$parquet_mr_version"
    implementation "org.apache.parquet:parquet-avro:$parquet_mr_version"
    implementation "org.apache.parquet:parquet-hadoop:$parquet_mr_version"

    implementation "com.google.cloud:spring-cloud-gcp-starter-storage:$spring_cloud_gcp_version"
    implementation "com.google.cloud:spring-cloud-gcp-starter-pubsub:$spring_cloud_gcp_version"

    // we need Hadoop libraries to read Parquet files for TDR import. However, Hadoop brings
    // a boatload of transitive dependencies we don't need, some of which (e.g. com.sun.xml.bind)
    // cause conflicts. Exclude a lot of the transitive deps. Inspired by https://stackoverflow.com/a/60067027
    def withoutHadoopExcludes = {
        exclude(group: 'log4j')
        exclude(group: 'org.slf4j')
        exclude(group: 'io.netty')
        exclude(group: 'org.mortbay.jetty')
        exclude(group: 'javax.servlet')
        exclude(group: 'javax.servlet.jsp')
        exclude(group: 'com.sun.jersey')
        exclude(group: 'com.sun.xml.bind')
        exclude(group: 'org.apache.curator')
        exclude(group: 'org.apache.zookeeper')
        exclude(group: 'org.apache.kerby')
        exclude(group: 'com.google.protobuf')
        exclude(group: 'org.bouncycastle')
        exclude(group: 'dnsjava')
    }
    implementation "org.apache.hadoop:hadoop-common:$hadoop_version", withoutHadoopExcludes
    implementation "org.apache.hadoop:hadoop-mapreduce-client-core:$hadoop_version", withoutHadoopExcludes

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    runtimeOnly 'org.webjars.npm:swagger-ui-dist:5.20.0'
    testImplementation 'io.micrometer:micrometer-observation-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation project(':client')
    testImplementation 'au.com.dius.pact.provider:spring:4.6.17'
    testImplementation 'au.com.dius.pact.consumer:junit5:4.6.17'
    testImplementation 'org.junit-pioneer:junit-pioneer:2.3.0'
    testImplementation 'org.awaitility:awaitility'

    // google bucket mock
    testImplementation 'com.google.cloud:google-cloud-nio:0.127.32'

    constraints {
        implementation('org.json:json:20250107') {
            because("CVE-2022-45688")
        }
        // commons-compress required by org.apache.avro:avro:1.11.3, required by java-pfb-library:0.14.0
        implementation('org.apache.commons:commons-compress:1.27.1') {
            because("CVE-2023-42503")
        }
        implementation('org.apache.commons:commons-configuration2:2.11.0') {
            because("CVE-2024-29131, CVE-2024-29133")
        }
    }
}

// generate server stubs, for WDS's v1 APIs only
tasks.register('generateApiInterfaces', GenerateTask) {
    generatorName = "spring"
    library = "spring-boot"
    inputSpec = "$rootDir/service/src/main/resources/static/swagger/apis-v1.yaml"
    outputDir = "$rootDir/service"
    cleanupOutput = false // else would delete the /service directory
    apiPackage = "org.databiosphere.workspacedataservice.generated"
    // apiNameSuffix does not work; see https://github.com/OpenAPITools/openapi-generator/issues/8822
    // apiNameSuffix = "Server" // disambiguate from classes in the generated client
    modelPackage = "org.databiosphere.workspacedataservice.generated"
    modelNameSuffix = "ServerModel"   // disambiguate from classes in the generated client
    apiFilesConstrainedTo.set([""])   // empty string means generate all apis
    modelFilesConstrainedTo.set([""]) // empty string means generate all models
    supportingFilesConstrainedTo.set([]) // empty array means generate none
    skipOperationExample = true // example responses require the ApiUtil.java supporting file
    configOptions.set([
        useJakartaEe           : "true", // for Spring Boot 3
        interfaceOnly          : "true", // Java interfaces only; no controllers or implementation classes
        useTags                : "true", // use the OpenAPI tag to classify apis, not the first segment of the api path
        hideGenerationTimestamp: "true"  // hidden to prevent unnecessary diff noise on unrelated changes
    ])

}


boolean isCiServer = System.getenv().containsKey("CI") && System.getenv().get("CI").toBoolean()

// kill switch for spotless, intended for one-off contingencies only, like when
// making changes which necessarily can't be formatted by spotless, like the initial
// commit of its config
boolean spotlessEnabled = true

spotless {
    java {
        targetExclude "${layout.buildDirectory.asFile}/**"
        targetExclude "**/swagger-code/**"
        targetExclude "**/generated*/**"
        googleJavaFormat('1.18.1')
        toggleOffOn() // allow spotless:off & spotless:on to protect code from formatting
    }
}
spotlessJavaCheck.onlyIf({ spotlessEnabled })

// multiple gradle tasks need generateApiInterfaces and generateGrammarSource to run before they do
generateGrammarSource.dependsOn(generateApiInterfaces)
spotlessJava.dependsOn(generateGrammarSource)
tasks.withType(ProcessResources).configureEach {
    it.dependsOn(generateGrammarSource)
}

// Run spotless check when running in github actions, otherwise run spotless apply.
compileJava {
    if (spotlessEnabled) {
        if (isCiServer) {
            dependsOn(spotlessCheck)
        } else {
            dependsOn(spotlessApply)
        }
    }
}

sonar {
    properties {
        property "sonar.projectName", "terra-workspace-data-service"
        property "sonar.projectKey", "DataBiosphere_workspace-data-service"
        property "sonar.organization", "broad-databiosphere"
        property "sonar.host.url", "https://sonarcloud.io"
        // exclude generated files from Sonar scans
        property "sonar.exclusions", "**/generated/*"
    }
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events = ["passed", "failed", "skipped", "started"]
        exceptionFormat = "full"
        // should command-line test output show stdout/stderr?
        showStandardStreams = false
    }
}

jacocoTestReport {
    reports {
        // sonarqube requires XML coverage output to upload coverage data
        xml.required = true
    }
}

test {
    systemProperties['pact.rootDir'] = layout.buildDirectory.dir("pacts").get().asFile.toString()
    systemProperties['pact.provider.version'] = "$project.version"
    systemProperties['user.timezone'] = "UTC"
}

tasks.register("wdsPactTests", Test) {
    environment "CONSUMER_NAME", "wds"
    useJUnitPlatform {
        includeTags "pact-test"
    }
    testLogging {
        showStandardStreams = true
    }
}

tasks.register("cwdsPactTests", Test) {
    environment "CONSUMER_NAME", "cwds"
    useJUnitPlatform {
        includeTags "pact-test"
    }
    testLogging {
        showStandardStreams = true
    }
}

tasks.register("pactTests") {
    dependsOn "wdsPactTests"
    dependsOn "cwdsPactTests"
}

generateGrammarSource {
    arguments += [
        "-visitor", // generate a visitor class
        "-package", "org.databiosphere.workspacedataservice.expressions.parser.antlr",
        "-Werror" // treat warnings as errors
    ]
}
