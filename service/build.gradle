plugins {
	id 'org.springframework.boot'
	id 'com.google.cloud.tools.jib'
	id 'org.hidetake.swagger.generator'
	id 'org.sonarqube'
	id 'com.gorylenko.gradle-git-properties'
	id 'jacoco'
	id "au.com.dius.pact" version "4.3.10"
}

springBoot {
	buildInfo()
}

repositories {
	maven { url 'https://broadinstitute.jfrog.io/artifactory/maven-central' }
	mavenCentral()
	maven { url 'https://broadinstitute.jfrog.io/artifactory/libs-snapshot' }
	maven { url 'https://broadinstitute.jfrog.io/artifactory/libs-release' }
}

jib {
	from {
		// see https://github.com/broadinstitute/dsp-appsec-blessed-images/tree/main/jre
		image = "us.gcr.io/broad-dsp-gcr-public/base/jre:17-distroless"
	}

	container {
		mainClass = 'org.databiosphere.workspacedataservice.WorkspaceDataServiceApplication'
		creationTime = "USE_CURRENT_TIMESTAMP"
	}
}

dependencies {
	implementation 'com.microsoft.azure:applicationinsights-runtime-attach:3.4.9'
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.retry:spring-retry:1.3.4'
	implementation 'org.aspectj:aspectjweaver:1.8.9' // required by spring-retry, not used directly by WDS
	implementation 'org.apache.commons:commons-lang3'
	implementation 'org.apache.commons:commons-csv:1.9.0'
	implementation 'com.google.guava:guava:31.1-jre'
	implementation 'org.postgresql:postgresql'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.webjars:webjars-locator-core:0.52'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.14.2'
	implementation 'io.sentry:sentry-logback:6.12.1'
	implementation 'com.azure:azure-identity:1.8.2'
	implementation 'org.liquibase:liquibase-core:4.21.1'
	implementation 'javax.cache:cache-api'
	implementation 'org.ehcache:ehcache:3.10.8'

	// Terra libraries
	implementation group: 'org.broadinstitute.dsde.workbench', name: 'sam-client_2.13', version: '0.1-08b6588'
	implementation 'com.squareup.okhttp3:okhttp:4.10.0' // required by Sam client
	implementation "bio.terra:datarepo-client:1.476.0-SNAPSHOT"
	implementation "bio.terra:workspace-manager-client:0.254.717-SNAPSHOT"

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	runtimeOnly 'org.webjars.npm:swagger-ui-dist:4.12.0'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
	testImplementation project(':client')
	testImplementation 'au.com.dius.pact.consumer:junit5:4.1.7'

	constraints {
		implementation('org.json:json:20230227') {
			because("CVE-2022-45688")
		}
	}
}

sonarqube {
	properties {
		property "sonar.projectName", "terra-workspace-data-service"
		property "sonar.projectKey", "DataBiosphere_workspace-data-service"
		property "sonar.organization", "broad-databiosphere"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

tasks.named('test') {
	useJUnitPlatform()
	testLogging {
		events = ["passed", "failed", "skipped", "started"]
		exceptionFormat = "full"
		// should command-line test output show stdout/stderr?
		showStandardStreams = false
	}
}

jacocoTestReport {
	reports {
		// sonarqube requires XML coverage output to upload coverage data
		xml.required = true
	}
}

test {
	systemProperties['pact.rootDir'] = "$buildDir/pacts"
	systemProperties['pact.provider.version'] = "$project.version"
}

tasks.register("pactTests", Test) {
	useJUnitPlatform {
		includeTags "pact-test"
	}
}
