dependencies {
	implementation 'io.swagger.core.v3:swagger-annotations'
	runtimeOnly 'org.webjars.npm:swagger-ui-dist:4.10.0'
	swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli'

	// Versioned by Spring:
	implementation 'javax.validation:validation-api'
	implementation 'org.webjars:webjars-locator-core'
}

generateSwaggerCode {
	inputFile = file('src/main/resources/static/swagger/openapi-docs.yaml')
	language = 'spring'
	components = ['models', 'apis']
	additionalProperties = [
			modelPackage     : "${artifactGroup}.generated.model",
			apiPackage       : "${artifactGroup}.generated.api",
			dateLibrary      : 'java11', //required for generated code to compile
			interfaceOnly    : 'true',
			useTags          : 'true',
			springBootVersion: dependencyManagement.managedVersions['org.springframework.boot:spring-boot']
	]
	rawOptions = ['--import-mappings', "VersionProperties=${artifactGroup}.config.VersionProperties"]
}

String swaggerOutputSrc = "${generateSwaggerCode.outputDir}/src/main/java"

idea.module.generatedSourceDirs = [file(swaggerOutputSrc)]
sourceSets.main.java.srcDir swaggerOutputSrc
compileJava.dependsOn generateSwaggerCode