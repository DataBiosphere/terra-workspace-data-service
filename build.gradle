import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '3.3.4' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
    id 'com.google.cloud.tools.jib' version '3.4.3' apply false
    id "org.sonarqube" version "5.1.0.4882" apply false
    id 'idea'
    id "org.openapi.generator" version "7.9.0" apply false
    id 'java'
    id 'com.gorylenko.gradle-git-properties' version '2.4.2'
    id 'jacoco'
    id 'com.diffplug.spotless' version '6.25.0' apply false
}

repositories {
    maven { url 'https://broadinstitute.jfrog.io/artifactory/maven-central' }
    mavenCentral()
}

subprojects {
    group = 'org.databiosphere'
    version = '0.23.5-SNAPSHOT'

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jvm-test-suite'

    ext {
        artifactGroup = "${group}.${rootProject.name}"
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }
        dependencies {
            // no dependencyManagement overrides at this time
        }
    }
}

idea.project.jdkName = '17'

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events = ["passed", "failed", "skipped", "started"]
        exceptionFormat = "full"
    }
}

// to avoid prompts in Gradle build scans
if (hasProperty("buildScan")) {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
    }
}
