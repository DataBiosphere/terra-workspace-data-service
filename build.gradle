import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '3.5.7' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'com.google.cloud.tools.jib' version '3.4.3' apply false
    id "org.sonarqube" version "7.0.1.6134" apply false
    id 'idea'
    id "org.openapi.generator" version "7.17.0" apply false
    id 'java'
    id 'com.gorylenko.gradle-git-properties' version '2.5.3'
    id 'jacoco'
    id 'com.diffplug.spotless' version '8.0.0' apply false
}

repositories {
    maven { url 'https://us-central1-maven.pkg.dev/dsp-artifact-registry/maven-central/' }
    mavenCentral()
}

subprojects {
    group = 'org.databiosphere'
    version = '0.38.33-SNAPSHOT'

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jvm-test-suite'

    ext {
        artifactGroup = "${group}.${rootProject.name}"
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }
        dependencies {
            // no dependencyManagement overrides at this time
        }
    }
}

idea.project.jdkName = '17'

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events = ["passed", "failed", "skipped", "started"]
        exceptionFormat = "full"
    }
}

// to avoid prompts in Gradle build scans
if (hasProperty("buildScan")) {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
    }
}
