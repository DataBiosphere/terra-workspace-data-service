import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '3.2.2' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id 'com.google.cloud.tools.jib' version '3.2.1' apply false
    id "org.sonarqube" version "4.4.1.3373" apply false
    id 'idea'
    id "org.openapi.generator" version "6.6.0" apply false
    id 'java'
    id 'com.gorylenko.gradle-git-properties' version '2.4.1'
    id 'jacoco'
    id 'com.diffplug.spotless' version '6.23.0' apply false
}

repositories {
    maven { url 'https://broadinstitute.jfrog.io/artifactory/maven-central' }
    mavenCentral()
}

subprojects {
    group = 'org.databiosphere'
    version = '0.2.113-SNAPSHOT'

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jvm-test-suite'

    ext {
        artifactGroup = "${group}.${rootProject.name}"
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }
        dependencies {
            // WDS requires a version of Liquibase with
            // https://github.com/liquibase/liquibase/pull/5391
            // "This bug existed from 4.22.0 to 4.25.1"
            dependency 'org.liquibase:liquibase-core:4.26.0'
            dependency 'com.jayway.jsonpath:json-path:2.9.0' // CVE-2023-51074
        }
    }
}

idea.project.jdkName = '17'

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events = ["passed", "failed", "skipped", "started"]
        exceptionFormat = "full"
    }
}
