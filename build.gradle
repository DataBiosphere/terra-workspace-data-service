import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '3.2.0' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id 'com.google.cloud.tools.jib' version '3.2.1' apply false
    id "org.sonarqube" version "4.4.1.3373" apply false
    id 'idea'
    id "org.openapi.generator" version "6.6.0" apply false
    id 'java'
    id 'com.gorylenko.gradle-git-properties' version '2.4.1'
    id 'jacoco'
    id 'com.diffplug.spotless' version '6.23.0' apply false
}

repositories {
    maven { url 'https://broadinstitute.jfrog.io/artifactory/maven-central' }
    mavenCentral()
}

subprojects {
    group = 'org.databiosphere'
    version = '0.2.111-SNAPSHOT'

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jvm-test-suite'

    ext {
        artifactGroup = "${group}.${rootProject.name}"
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }

        dependencies {
            dependency 'org.yaml:snakeyaml:2.0'
            dependency 'org.webjars:webjars-locator-core:0.53'
            dependency 'ch.qos.logback:logback-classic:1.2.13+' // CVE-2023-6378
            dependency 'ch.qos.logback:logback-core:1.2.13+' // CVE-2023-6378
        }
    }
}

idea.project.jdkName = '17'

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events = ["passed", "failed", "skipped", "started"]
        exceptionFormat = "full"
    }
}
