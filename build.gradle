import org.hidetake.gradle.swagger.generator.GenerateSwaggerCode
import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.7.11' apply false
    id 'io.spring.dependency-management' version '1.1.0' apply false
    id 'com.google.cloud.tools.jib' version '3.2.1' apply false
    id "org.sonarqube" version "4.0.0.2929" apply false
    id 'idea'
    id 'org.hidetake.swagger.generator' version '2.19.2' apply false
    id 'java'
    id 'com.gorylenko.gradle-git-properties' version '2.4.1'
    id 'jacoco'
}

repositories {
    maven { url 'https://broadinstitute.jfrog.io/artifactory/maven-central' }
    mavenCentral()
}

subprojects {
    group = 'org.databiosphere'
    version = '0.2.79-SNAPSHOT'

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'org.hidetake.swagger.generator'

    ext {
        artifactGroup = "${group}.${rootProject.name}"
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }

        dependencies {
            dependency 'io.swagger.core.v3:swagger-annotations:2.1.13'
            dependency 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.34'
            dependency 'org.yaml:snakeyaml:2.0'
        }
    }

    tasks.withType(GenerateSwaggerCode).all {
        jvmArgs = ['--add-opens=java.base/java.util=ALL-UNNAMED'] // for jvm 16+
    }
}

idea.project.jdkName = '17'

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events = ["passed", "failed", "skipped", "started"]
        exceptionFormat = "full"
    }
}
