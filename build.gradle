import org.hidetake.gradle.swagger.generator.GenerateSwaggerCode
import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.6.8' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'com.google.cloud.tools.jib' version '3.2.1' apply false
    id "org.sonarqube" version "3.4.0.2513" apply false
    id 'idea'
    id 'org.hidetake.swagger.generator' version '2.19.2' apply false
    id 'java'
    id 'com.diffplug.spotless' version '6.8.0'
}

repositories { mavenCentral() }

subprojects {
    group = 'org.databiosphere'
    version = '0.1.30-SNAPSHOT'

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'org.hidetake.swagger.generator'

    ext {
        artifactGroup = "${group}.${rootProject.name}"
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }

        dependencies {
            dependency 'io.swagger.core.v3:swagger-annotations:2.1.13'
            dependency 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.34'
        }
    }

    tasks.withType(GenerateSwaggerCode).all {
        jvmArgs = ['--add-opens=java.base/java.util=ALL-UNNAMED'] // for jvm 16+
    }
}

idea.project.jdkName = '17'

// Spotless configuration
spotless {
    java {
        eclipse()
        target '**/*.java'
        targetExclude("${buildDir}/**", "**/generated/**")
    }
    format 'misc', {
        // define the files to apply `misc` to
        target '*.gradle', '*.md', '.gitignore'

        // N.B. checks for tabs vs. spaces but does not check the number of spaces
        // https://github.com/diffplug/spotless/issues/794
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

build.dependsOn spotlessApply

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events = ["passed", "failed", "skipped", "started"]
        exceptionFormat = "full"
    }
}
